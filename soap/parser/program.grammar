# =========
#  program
# =========

statement =
    compound_statement /
    boolean_expression /
    arithmetic_expression /
    _  # empty
compound_statement = single_statement statement
single_statement =
    skip_statement /
    assign_statement /
    if_statement /
    while_statement /
    input_statement /
    output_statement

skip_statement = skip semicolon

assign_statement =
    variable assign arithmetic_expression semicolon

if_statement = if_block semicolon
if_block = if_else_block / if_then_block
if_then_block = if boolean_block code_block
if_else_block = if_then_block code_block

while_statement = while boolean_block code_block semicolon

boolean_block = left_paren boolean_expression right_paren
code_block = left_paren statement right_paren

input_statement = input left_paren input_list right_paren semicolon
input_list = input_expr maybe_input_list
maybe_input_list = comma_input_list / _
comma_input_list = comma input_list
input_expr = variable colon number

output_statement = output left_paren output_list right_paren semicolon
output_list = variable maybe_output_list
maybe_output_list = comma_output_list / _
comma_output_list = comma output_list


# =============
#  expressions
# =============

boolean_expression = and_factor maybe_or_list

and_factor   = bool_atom maybe_and_list
bool_atom    = bool_parened / binary_bool_expr / unary_bool_expr
bool_parened = left_paren boolean_expression right_paren

binary_bool_expr = term compare_operator term
unary_bool_expr  = not bool_atom
maybe_or_list    = or_list / _
or_list          = or_expr maybe_or_list
or_expr          = or and_factor
maybe_and_list   = and_list / _
and_list         = and_expr maybe_and_list
and_expr         = and bool_atom

arithmetic_expression = select / term

select  = boolean_expression question term colon term
term    = factor maybe_sum_list
factor  = atom maybe_prod_list
atom    = parened / unary_expr / number / variable
parened = left_paren arithmetic_expression right_paren

unary_expr      = sub atom
maybe_sum_list  = sum_list / _
sum_list        = sum_expr maybe_sum_list
sum_expr        = sum_operator factor
maybe_prod_list = prod_list / _
prod_list       = prod_expr maybe_prod_list
prod_expr       = prod_operator atom

number = error / interval / scalar
error = interval interval
interval = left_brac scalar comma scalar right_brac
scalar = float / integer


# ==========
#  literals
# ==========

skip        = _ 'skip'   _
if          = _ 'if'     _
while       = _ 'while'  _
input       = _ 'input'  _
output      = _ 'output' _

assign      = _ ':=' _
left_paren  = _ '('  _
right_paren = _ ')'  _
semicolon   = _ ';'  _
question    = _ '?'  _
colon       = _ ':'  _

compare_operator = le / lt / ge / gt / eq / ne

lt = _ '<'  _
le = _ '<=' _
ge = _ '>=' _
gt = _ '>'  _
eq = _ '==' _
ne = _ '!=' _

and = _ 'and' _
or  = _ 'or'  _
not = _ 'not' _

sum_operator = add / sub
prod_operator = mul / div / pow

add = _ '+' _
sub = _ '-' _
mul = _ '*' _
div = _ '/' _
pow = _ '^' _

left_brac  = _ '[' _
right_brac = _ ']' _
comma      = _ ',' _


# =========
#  regexes
# =========

variable = _ variable_regex _
variable_regex = ~'[a-zA-Z_][a-zA-Z_0-9]*'

integer = _ integer_regex _
float = _ float_regex _
integer_regex = ~'[0-9]+'
float_regex = ~'(\+|-)?([0-9]+\.?[0-9]*|\.[0-9]+)([eE](\+|-)?[0-9]+)?'

_ = ~r"\s*(?:#[^\r\n]*)?\s*"
